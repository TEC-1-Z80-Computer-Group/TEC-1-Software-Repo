Sample Tiny Basic Code for the SN76489 programmable sound generator 
-------------------------------------------------------------------

Basic code to demonstrate the SN76489 sound generator capabilities.  
Translated to work on BMON (TEC-1F) with Tiny Basic.  Code directly
from the "Applications Report" for the SN76489.  Note: OUT command
could be different on your system.

---------

10  REM ** BELL OR CHIME **
20  N=7; REM PORT NUMBER OF SN76489
30  OUT N, 159; REM TURN OFF TONE 1
40  OUT N, 191; REM TURN OFF TONE 3
50  OUT N, 223; REM TURN OFF TONE 4
60  OUT N, 255; REM TURN OFF NOISE
70  OUT N, 140; REM TONE 1 AT 679HZ
80  OUT N, 5
90  OUT N, 170; REM TONE 2 AT 694HZ
100 OUT N, 5
110 FOR B=0 TO 11; REM B=NUMBER OF BELLS
120 FOR I=145 TO 159; REM LOOP TO GENERATE VOLUME STEPS
130 OUT N, I; OUT N, (I+32); REM CHANGE VOLUME FOR TONE 1&2
140 FOR D=0 TO 75; NEXT D; REM DELAY LOOP
150 NEXT I
160 NEXT B
190 PRINT "END OF SOUND"

---------

10  REM ** BASIC BIRD SOUND **
20  N=7; REM PORT NUMBER OF SN76489
30  OUT N, 159; REM TURN OFF TONE 1
40  OUT N, 191; REM TURN OFF TONE 3
50  OUT N, 223; REM TURN OFF TONE 4
60  OUT N, 255; REM TURN OFF NOISE
70  Z=RND(10); REM CHIRP LENGTH 1-10
80  OUT N, 144; REM SET TONE 1 VOLUME
90  FOR I=0 TO 15; REM START CHIRP LOOP
100 OUT N, (128+I); REM FREQ FROM 3906-2016 HZ
110 OUT N, 1
120 FOR D=0 TO Z; NEXT D; REM DELAY BY RND AMOUNT
130 NEXT I
140 S=S+Z; REM COUNTER
150 IF S < 200 GOTO 70
160 OUT N, 159; REM TURN OFF TONE 1

---------

10  REM ** MISSILE SOUND **
20  N=7; REM PORT NUMBER OF SN76489
30  OUT N, 159; REM TURN OFF TONE 1
40  OUT N, 191; REM TURN OFF TONE 3
50  OUT N, 223; REM TURN OFF TONE 4
60  OUT N, 255; REM TURN OFF NOISE
70  OUT N, 231; REM SET NOISE TONE
80  OUT N, 240; REM SET NOISE VOLUME
90  FOR B=0 TO 15; REM LOOP FOR SECOND BYTE
100 FOR A=192 TO 207;REM LOOP FOR FIRST BYTE
110 OUT N, A; OUT N, B; REM OUTPUT TONES
120 NEXT A
130 OUT N, (240+B); REM DECREASE VOLUME
140 NEXT B
150 GOTO 30

---------

10  REM ** BOMB DROP AND EXPLOSION **
20  N=7; REM PORT NUMBER OF SN76489
30  OUT N, 159; REM TURN OFF TONE 1
40  OUT N, 191; REM TURN OFF TONE 3
50  OUT N, 223; REM TURN OFF TONE 4
60  OUT N, 255; REM TURN OFF NOISE
70  OUT N, 144; REM SET TONE 1 VOLUME
80  FOR J=5 TO 17; REM BYTE 2 SWEEP
90  FOR I=128 TO 143; REM BYTE 1 SWEEP
100 OUT N, I; OUT N, J; REM OUTPUT TONES
110 FOR D=0 TO 10; NEXT D; REM DELAY LOOP
120 NEXT I
130 NEXT J
140 OUT N, 159; REM TURN TONE 1 OFF
150 OUT N, 228; REM SET NOISE TO HIGH PITCH
160 FOR I=240 TO 255; REM SET NOISE VOLUME
170 OUT N, I; REM OUTPUT NOISE
180 FOR D=0 TO 75; NEXT D; REM DELAY LOOP
190 NEXT I
